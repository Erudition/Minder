module Task.AssignedAction exposing (..)

import Activity.Activity exposing (ActivityID)
import Dict exposing (Dict)
import Incubator.IntDict.Extra as IntDict
import IntDict exposing (IntDict)
import Json.Decode.Exploration as Decode exposing (..)
import Json.Decode.Exploration.Pipeline as Pipeline exposing (..)
import Json.Encode as Encode exposing (..)
import Json.Encode.Extra as Encode2 exposing (..)
import Result.Extra as Result
import SmartTime.Duration exposing (Duration)
import SmartTime.Human.Clock as Clock
import SmartTime.Human.Moment as HumanMoment exposing (FuzzyMoment, Zone)
import SmartTime.Moment exposing (..)
import SmartTime.Period as Period exposing (Period)
import Task.ActionClass exposing (ActionClass, ActionClassID, ActionClassSkel, ParentProperties, decodeActionClassID, decodeTaskMoment, encodeTaskMoment)
import Task.Entry exposing (Entry, getClassesFromEntries)
import Task.Progress as Progress exposing (..)
import Task.Series exposing (Series, SeriesID)
import Task.SessionSkel exposing (UserPlannedSession, decodeSession, encodeSession)
import ZoneHistory exposing (ZoneHistory)



-- AssignedAction Skeleton (bare minimum, non-derivative data, saved to disk) --------------------------------


{-| Definition of a single instance of a single task - one particular time that the specific thing will be done, that can be scheduled. Can be thought of as an "assignment" of a task (class). There may be zero (an unassigned task), and there may be many (a repeated task) for a given class.
-}
type alias AssignedActionSkel =
    { class : ActionClassID
    , id : AssignedActionID
    , memberOfSeries : Maybe SeriesID
    , completion : Progress.Portion
    , externalDeadline : Maybe FuzzyMoment -- *
    , startBy : Maybe FuzzyMoment -- *
    , finishBy : Maybe FuzzyMoment -- *
    , plannedSessions : List UserPlannedSession
    , relevanceStarts : Maybe FuzzyMoment -- *
    , relevanceEnds : Maybe FuzzyMoment -- * (*)=An absolute FuzzyMoment if specified, otherwise generated by relative rules from class
    , extra : Dict String String
    }


decodeAssignedActionSkel : Decode.Decoder AssignedActionSkel
decodeAssignedActionSkel =
    decode AssignedActionSkel
        |> Pipeline.required "class" decodeActionClassID
        |> Pipeline.required "id" decodeAssignedActionID
        |> Pipeline.optional "memberOfSeries" (Decode.nullable Decode.int) Nothing
        |> Pipeline.required "completion" Decode.int
        |> Pipeline.required "externalDeadline" (Decode.nullable decodeTaskMoment)
        |> Pipeline.required "startBy" (Decode.nullable decodeTaskMoment)
        |> Pipeline.required "finishBy" (Decode.nullable decodeTaskMoment)
        |> Pipeline.required "plannedSessions" (Decode.list decodeSession)
        |> Pipeline.required "relevanceStarts" (Decode.nullable decodeTaskMoment)
        |> Pipeline.required "relevanceEnds" (Decode.nullable decodeTaskMoment)
        |> Pipeline.optional "extra" (Decode.dict Decode.string) Dict.empty


encodeAssignedActionSkel : AssignedActionSkel -> Encode.Value
encodeAssignedActionSkel taskInstance =
    Encode.object <|
        [ ( "class", Encode.int taskInstance.class )
        , ( "id", Encode.int taskInstance.id )
        , ( "memberOfSeries", Encode2.maybe Encode.int taskInstance.memberOfSeries )
        , ( "completion", Encode.int taskInstance.completion )
        , ( "externalDeadline", Encode2.maybe encodeTaskMoment taskInstance.externalDeadline )
        , ( "startBy", Encode2.maybe encodeTaskMoment taskInstance.startBy )
        , ( "finishBy", Encode2.maybe encodeTaskMoment taskInstance.finishBy )
        , ( "plannedSessions", Encode.list encodeSession taskInstance.plannedSessions )
        , ( "relevanceStarts", Encode2.maybe encodeTaskMoment taskInstance.relevanceStarts )
        , ( "relevanceEnds", Encode2.maybe encodeTaskMoment taskInstance.relevanceEnds )
        , ( "extra", Encode.dict identity Encode.string taskInstance.extra )
        ]


newAssignedActionSkel : Int -> ActionClassSkel -> AssignedActionSkel
newAssignedActionSkel newID class =
    { class = class.id
    , id = newID
    , memberOfSeries = Nothing
    , completion = 0
    , externalDeadline = Nothing
    , startBy = Nothing
    , finishBy = Nothing
    , plannedSessions = []
    , relevanceStarts = Nothing
    , relevanceEnds = Nothing
    , extra = Dict.empty
    }


type alias AssignedActionID =
    Int


decodeAssignedActionID : Decoder AssignedActionID
decodeAssignedActionID =
    Decode.int


encodeAssignedActionID : AssignedActionID -> Encode.Value
encodeAssignedActionID taskInstanceID =
    Encode.int taskInstanceID



-- FULL Instances (augmented with Entry & ActionClass) ----------------------------------------


{-| A fully spec'ed-out version of a TaskInstance
-}
type alias AssignedAction =
    { parents : List ParentProperties
    , class : ActionClassSkel
    , instance : AssignedActionSkel
    , index : Int
    }


{-| Get all relevant instances of everything.

Take the skeleton data and get all relevant(within given time period) instances of every class, and return them as Full Instances.

TODO organize with IDs somehow

-}
listAllAssignedActions : List ActionClass -> IntDict.IntDict AssignedActionSkel -> ( ZoneHistory, Period ) -> List AssignedAction
listAllAssignedActions fullClasses savedInstanceSkeletons timeData =
    List.concatMap (assignedActionsOfClass timeData savedInstanceSkeletons) fullClasses


{-| Take a class and return all of the instances relevant within the given period - saved or generated.

Combine the saved instances with generated ones, to get the full picture within a period.

TODO: best data structure? Is Dict unnecessary here? Or should the key involve the classID for perf?

-}
assignedActionsOfClass : ( ZoneHistory, Period ) -> IntDict AssignedActionSkel -> ActionClass -> List AssignedAction
assignedActionsOfClass ( zoneHistory, relevantPeriod ) allSavedInstances fullClass =
    let
        -- Any & all saved instances that match this taskclass
        -- TODO more efficient way to filter?
        savedInstancesWithMatchingClass =
            List.filter (\instance -> instance.class == fullClass.class.id) (IntDict.values allSavedInstances)

        savedInstancesFull =
            List.indexedMap toFull savedInstancesWithMatchingClass

        toFull : Int -> AssignedActionSkel -> AssignedAction
        toFull indexFromZero instanceSkel =
            { parents = fullClass.parents
            , class = fullClass.class
            , instance = instanceSkel
            , index = indexFromZero + 1
            }

        -- Filter out instances outside the window
        relevantSavedInstances =
            List.filter isRelevant savedInstancesFull

        -- TODO "If savedInstance is within period, keep"
        isRelevant savedInstance =
            True

        -- TODO Fill in based on recurrence series. Int ID = order in series.
        relevantSeriesMembers =
            fillSeries ( zoneHistory, relevantPeriod ) fullClass fullClass.recurrence
    in
    relevantSavedInstances
        ++ relevantSeriesMembers


fillSeries : ( ZoneHistory, Period ) -> ActionClass -> Maybe Series -> List AssignedAction
fillSeries ( zoneHistory, relevantPeriod ) fullClass seriesRule =
    -- TODO
    []


instanceProgress : AssignedAction -> Progress
instanceProgress fullInstance =
    ( fullInstance.instance.completion, fullInstance.class.completionUnits )



-- Task helper functions ---------------------------------------------


isRelevantNow : AssignedAction -> Moment -> Zone -> Bool
isRelevantNow instance now zone =
    let
        fuzzyNow =
            HumanMoment.Global now

        start =
            Maybe.withDefault fuzzyNow instance.instance.relevanceStarts

        end =
            Maybe.withDefault fuzzyNow instance.instance.relevanceEnds

        notBeforeStart =
            HumanMoment.compareFuzzy zone Clock.startOfDay fuzzyNow start /= Earlier

        notAfterEnd =
            HumanMoment.compareFuzzy zone Clock.endOfDay fuzzyNow end /= Later
    in
    notBeforeStart && notAfterEnd


completed : AssignedAction -> Bool
completed spec =
    isMax ( spec.instance.completion, spec.class.completionUnits )


partiallyCompleted : AssignedAction -> Bool
partiallyCompleted spec =
    spec.instance.completion > 0


type alias WithSoonness t =
    { t | soonness : Duration }


prioritize : Moment -> HumanMoment.Zone -> List AssignedAction -> List AssignedAction
prioritize now zone taskList =
    let
        -- lowest values first
        compareProp prop a b =
            Basics.compare (prop a) (prop b)

        -- highest values first
        comparePropInverted prop a b =
            Basics.compare (prop b) (prop a)
    in
    -- deepSort [ compareSoonness zone, comparePropInverted .importance ] taskList
    deepSort [ compareSoonness zone ] taskList



-- List.sortWith (compareSoonness zone) <| List.sortBy .importance taskList


type alias CompareFunction a =
    a -> a -> Basics.Order


deepSort : List (CompareFunction a) -> List a -> List a
deepSort compareFuncs listToSort =
    let
        deepCompare funcs a b =
            case funcs of
                [] ->
                    -- No more comparisons to make, give up and say they're equal
                    EQ

                nextCompareFunc :: laterCompareFuncs ->
                    let
                        -- run next comparison
                        check =
                            nextCompareFunc a b
                    in
                    if check == EQ then
                        -- they still look equal, dig deeper
                        deepCompare laterCompareFuncs a b

                    else
                        -- we have a winner, we can stop digging
                        check
    in
    List.sortWith (deepCompare compareFuncs) listToSort


{-| TODO this could be a Moment.Fuzzy function
-}
compareSoonness : HumanMoment.Zone -> CompareFunction AssignedAction
compareSoonness zone taskA taskB =
    case ( taskA.instance.externalDeadline, taskB.instance.externalDeadline ) of
        ( Just fuzzyMomentA, Just fuzzyMomentB ) ->
            HumanMoment.compareFuzzyLateness zone Clock.endOfDay fuzzyMomentA fuzzyMomentB

        ( Nothing, Nothing ) ->
            -- whenevers can't be compared
            EQ

        ( Just _, Nothing ) ->
            -- actual times always come before whenevers
            LT

        ( Nothing, Just _ ) ->
            -- whenevers always come after actual times
            GT


getID : AssignedAction -> AssignedActionID
getID ins =
    ins.instance.id


getTitle : AssignedAction -> String
getTitle ins =
    ins.class.title


getActivityID : AssignedAction -> Maybe ActivityID
getActivityID ins =
    ins.class.activity


getProgress : AssignedAction -> Progress
getProgress instance =
    ( instance.instance.completion, instance.class.completionUnits )


getProgressMaxInt : AssignedAction -> Portion
getProgressMaxInt instance =
    Progress.unitMax instance.class.completionUnits


getCompletionInt : AssignedAction -> Int
getCompletionInt instance =
    instance.instance.completion


getExtra : String -> AssignedAction -> Maybe String
getExtra key instance =
    Dict.get key instance.instance.extra
